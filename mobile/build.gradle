// Main build script for WebView App
// 
// Usage: gradlew assembleDebug           Build debug APK
//        gradlew assembleRelease         Build release APK

final VERSION_MAJOR = 2 // max two digits
final VERSION_MINOR = 5 // max two digits
final VERSION_PATCH = 0 // max two digits
final VERSION_BUILD = 0 // max three digits

apply plugin: "com.android.application"
apply from: "../utils.gradle"

android {
	compileSdkVersion 28
	buildToolsVersion "28.0.3"

	defaultConfig {
		applicationId "com.robotemplates.webviewapp"
		minSdkVersion 16
		targetSdkVersion 28
		versionCode getVersionCode(VERSION_MAJOR, VERSION_MINOR, VERSION_PATCH, VERSION_BUILD)
		versionName getVersionName(VERSION_MAJOR, VERSION_MINOR, VERSION_PATCH)
	}

	signingConfigs {
		release {
			// passwords and alias are obtained via askForPasswords task
			storeFile file("../${project.property('keystore.file')}")
			storePassword ""
			keyAlias ""
			keyPassword ""
		}
	}

	buildTypes {
		debug {
			buildConfigField "boolean", "LOGS", "true"
			buildConfigField "boolean", "DEV_ENVIRONMENT", "true"

			versionNameSuffix "-debug"
		}

		release {
			buildConfigField "boolean", "LOGS", "false"
			buildConfigField "boolean", "DEV_ENVIRONMENT", "false"

			signingConfig signingConfigs.release
			zipAlignEnabled true
			minifyEnabled false
			shrinkResources false
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_7
		targetCompatibility JavaVersion.VERSION_1_7
	}

	applicationVariants.all {
		variant ->
			if (variant.buildType.name.equals("release")) {
				renameArtifact("webviewapp", variant)
			}
	}
}

dependencies {
	implementation fileTree(dir: "libs", include: ["*.jar"])
	implementation(name: "config", ext: "aar")
	implementation "com.android.support:appcompat-v7:28.0.0"
	implementation "com.android.support:customtabs:28.0.0"
	implementation "com.android.support:design:28.0.0"
	implementation "com.android.support:support-compat:28.0.0"
	implementation "com.android.support:support-core-ui:28.0.0"
	implementation "com.android.support:support-core-utils:28.0.0"
	implementation "com.android.support:support-fragment:28.0.0"
	implementation "com.android.support:support-v4:28.0.0"
	implementation "com.google.android.ads.consent:consent-library:1.0.6"
	implementation "com.google.firebase:firebase-ads:16.0.1"
	implementation "com.google.firebase:firebase-core:16.0.5"
	implementation "com.google.firebase:firebase-messaging:17.3.4"
	implementation "org.alfonz:alfonz-utility:0.9.0"
	implementation "org.alfonz:alfonz-view:0.9.0"
}

apply plugin: "com.google.gms.google-services"
